apply plugin: 'com.android.library'

group = 'com.youversion.reactnativesdk'
version = '0.8.0'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
buildscript {
  repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
  }
  dependencies {
    classpath("org.jetbrains.kotlin.plugin.compose:org.jetbrains.kotlin.plugin.compose.gradle.plugin:2.1.20")
  }
}

// Simple helper that allows the root project to override versions declared by this library.
def safeExtGet = { prop, fallback ->
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 36)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 24)
      targetSdkVersion safeExtGet("targetSdkVersion", 36)
    }
  }
}

android {
  namespace "com.youversion.reactnativesdk"
  defaultConfig {
    versionCode 1
    versionName "0.8.0"
  }
  buildFeatures {
    compose true
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  implementation 'androidx.compose.ui:ui-android:1.9.1'
  implementation 'androidx.compose.foundation:foundation-android:1.9.1'
  implementation 'androidx.compose.material3:material3-android:1.3.2'

  implementation 'com.youversion.platform:platform-core:0.5.0'
  implementation 'com.youversion.platform:platform-ui:0.5.0'
  implementation 'com.youversion.platform:platform-reader:0.5.0'
}